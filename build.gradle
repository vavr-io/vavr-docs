buildscript {

    ext{
        asciiDoctorGradlePluginVersion="1.5.2"
        asciiDoctorJPdfVersion="1.5.0-alpha.10"
        asciiDoctorJVersion="1.5.3.1"
        gradleGitVersion="1.3.2"
        gradleWatchPluginVersion="0.1.5"
        livereloadGradlePluginVersion="0.2.1"
    }

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciiDoctorGradlePluginVersion}"
        classpath "org.asciidoctor:asciidoctorj-pdf:${asciiDoctorJPdfVersion}"
        classpath "org.asciidoctor:asciidoctorj:${asciiDoctorJVersion}"
        classpath "org.ajoberstar:gradle-git:${gradleGitVersion}"
        classpath "com.bluepapa32:gradle-watch-plugin:${gradleWatchPluginVersion}"
        classpath "org.kordamp.gradle:livereload-gradle-plugin:${livereloadGradlePluginVersion}"
    }
}
description = 'Javaslang Documentation'
version = '2.0.0-beta'
group = 'com.javaslang'

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.bluepapa32.watch'
apply plugin: 'org.kordamp.gradle.livereload'

repositories {
    jcenter()
    mavenCentral()
}

asciidoctor {
    sources {
        include 'index.adoc'
    }
    backends = ['html5','pdf']
}

githubPages {
    repoUri = 'https://github.com/javaslang/javaslang-docs.git'

    credentials {
        username = project.githubUser
        password = project.githubPassword
    }

    pages {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

watch {
    asciidoc {
        files fileTree('src/docs/asciidoc')
        tasks 'asciidoctor'
    }
}

liveReload {
    docRoot asciidoctor.outputDir.canonicalPath
}

publishGhPages.dependsOn asciidoctor

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
